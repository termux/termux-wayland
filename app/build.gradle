apply plugin: 'com.android.application'

android {
    namespace 'com.termux.x11'
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.termux.x11"
        minSdkVersion 26

        /*
         * Do not increase targetSdkVersion
         * Android does not allow to manage socket
         * owned by other userId if targetSdkVersion is 29+
         */
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 28
        versionCode 12
        versionName "1.02.10"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('testkey_untrusted.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    splits {
        abi {
            enable true
            reset()

            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    buildFeatures {
        aidl true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    packagingOptions {
        jniLibs {
            // This will allow us to use shared libraries inside *.apk, without unpacking
            // @agnostic-apollo is the best
            // https://github.com/termux/termux-x11/commit/6cdfb75c4451eef63f114a93baef5ba73b7cfd8c#commitcomment-77856313
            useLegacyPackaging false
        }

        exclude 'lib/*/liblocal-socket.so'
    }
}

dependencies {
    //noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.21"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.termux.termux-app:termux-shared:2f5a6f7de6'
    implementation 'com.google.android.material:material:1.9.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    compileOnly project(':shell-loader:stub')
}
